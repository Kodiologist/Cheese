#!/usr/bin/perl -T

my %p;
%p = @ARGV; s/,\z// foreach values %p; # DEPLOYMENT SCRIPT EDITS THIS LINE

#use warnings; BEGIN {@ARGV or open STDERR, '>', '/tmp/cgi-error.txt' or die};
use strict;

use lib '/home/dove/perl';
use Tversky qw(htmlsafe shuffle);

my %desserts =
   ('pbbrowcake.jpg' => 'Peanut-butter cup brownie cake',
    'wcbrule.jpg' => 'White-chocolate brûlée cheesecake',
    'ocheesecook.jpg' => 'Oreo cheecake cookies',
    'cookiepie.jpg' => 'Cookie-dough cheesecake',
    'oreocc.jpg' => 'Death by Oreo cupcakes',
    'bbyogurt.jpg' => 'Blackberry ice cream',
    'wlsorbet.jpg' => 'Watermelon lime sorbet',
    'doughnuts.jpg' => 'Assorted doughnuts',
    'rainbow.jpg' => 'Rainbow-swirl cookies',
    'bars.jpg' => 'Caramel-chocolate-marshmallow bars',
    'malt.jpg' => 'Malt-ball chocolate cake',
    'iaps-7405.jpg' => 'White frosted cupcakes');

# ------------------------------------------------

sub enum1
  {my $n = 0;
   return map {[++$n, $_]} @_;}

sub dessert_menu_page
   {my ($o, $choice, @ds) = @_;
    $o->multiple_choice_page($choice ? "choice$choice" : undef,
        sprintf('<p>%s of the %s desserts do you find most appealing?</p>',
            $choice < 2 ? 'Which' : 'Now, which',
            $choice < 2 ? 'below' : 'remaining'),
        map
           {my ($i, $file) = @$_;
            $i => sprintf '<div><img class="dessert" src="%s" alt="%s"></div><div class="dessert-description">%s</div>',
                htmlsafe($p{dessert_url_prefix} . $file),
                htmlsafe($desserts{$file}),
                htmlsafe($desserts{$file})}
        @ds);}

# ------------------------------------------------

print <DATA>; # Header

local $@;

eval

   {my $o = new Tversky
       (here_url => $p{here_url},
        database_path => $p{database_path},
        consent_path => $p{consent_path},
        task_version => $p{task_version},

        tables =>
           {subjects => 'Subjects', mturk => 'MTurk',
            timing => 'Timing', user => 'D'},

        preview => sub
           {my $o = shift;
            print '<p class="note">(This is only a preview. Accept the HIT before beginning to work.)</p>';
            dessert_menu_page $o, 0, enum1 shuffle keys %desserts;},

        after_consent_prep => sub
           {my $o = shift;
            $o->assign_permutation('dessert_permutation', '|',
                keys %desserts);},

        footer => "\n\n\n</body></html>\n",

        mturk => $p{mturk});

    foreach my $choice (1, 2, 3)
       {my @ds = enum1 split /\|/, $o->getu('dessert_permutation');
        for (my $prev_choice = 1 ; $prev_choice < $choice ; ++$prev_choice)
          # Remove previously-chosen items from the lineup.
           {my $d = $o->getu("choice$prev_choice");
            @ds = grep {$_->[0] != $d} @ds;}
        dessert_menu_page $o, $choice, @ds;}

    $o->completion_page;};

defined $@ or $@ = 'Hit bottom with undefined $@';
$@ eq '' and $@ = 'Hit bottom with $@ set to the null string';

warn $@;

print "<p>Error:</p><pre>", htmlsafe($@), "</pre><p>Please report this.</p>\n</body></html>\n";

__END__
Content-Type: text/html; charset=utf-8

<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01//EN"
   "http://www.w3.org/TR/html4/strict.dtd">
<html lang="en-US">
<head>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
    <title>Dessert Survey</title>
    <style type="text/css">

    h1, form, p.stimulus, div.expbody p
       {text-align: center;}

    div.expbody p.long
       {text-align: left;}

    p.note
       {font-style: italic;}

    input.consent_statement
       {border: thin solid black;
        background-color: white;
        color: black;
        margin-bottom: .5em;}

    div.multiple_choice_box
       {display: table;
        margin-left: auto; margin-right: auto;}
    div.multiple_choice_box > div.row
       {display: table-row;}
    div.multiple_choice_box > div.row > div
       {display: table-cell;}
    div.multiple_choice_box > div.row > div.button
       {vertical-align: middle;
        padding-right: 1em;}
    div.multiple_choice_box > div.row > div.body
       {text-align: left;
        max-width: 200 px;}
    div.multiple_choice_box > div.row > div.body > div
       {display: table-cell;
        vertical-align: middle;}

    img.dessert
       {border: thin solid black;}
    div.dessert-description
       {font-size: 35px;
        padding-left: 10px;}

   </style>
</head>

<body>
