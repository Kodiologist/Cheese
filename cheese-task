#!/usr/bin/perl -T

my %p;
%p = @ARGV; s/,\z// foreach values %p; # DEPLOYMENT SCRIPT EDITS THIS LINE

#use warnings; BEGIN {@ARGV or open STDERR, '>', '/tmp/cgi-error.txt' or die};
use strict;

use lib '/home/dove/perl';
use Tversky qw(htmlsafe shuffle);

my @pictures = qw(back.jpg bent.jpg crosses.jpg crotchwards.jpg curls.jpg deck.jpg mirror.jpg profile.jpg skintone.jpg stairs.jpg office.jpg shirt.jpg);

# ------------------------------------------------

sub enum1
  {my $n = 0;
   return map {[++$n, $_]} @_;}

sub menu_page
   {my ($o, $show_hint, $choice, @ps) = @_;
    $o->multiple_choice_page($choice ? "choice$choice" : undef,
        sprintf('%s<p>%s of the %s pictures do you find most appealing?</p>',
            $show_hint ? q(<p class="long"><strong>Hint:</strong> If the images appear too tall to fit on the screen, try viewing this frame in its own window. (In Firefox, you can accomplish this by right-clicking on the frame and choosing "This Frame" &rarr; "Show Only This Frame".) If you do this, you may get a blank page when you submit the HIT, but don't be alarmed: it should still have gone through.</p>) : '',
            $choice < 2 ? 'Which' : 'Now, which',
            $choice < 2 ? 'below' : 'remaining'),
        map
           {my ($i, $file) = @$_;
            $i => sprintf '<img class="cheesecake" src="%s" alt="Cheesecake image">',
                htmlsafe($p{cheesecake_url_prefix} . $file);}
        @ps);}

# ------------------------------------------------

print <DATA>; # Header

local $@;

eval

   {my $o = new Tversky
       (here_url => $p{here_url},
        database_path => $p{database_path},
        consent_path => $p{consent_path},
        task_version => $p{task_version},

        tables =>
           {subjects => 'Subjects', mturk => 'MTurk',
            timing => 'Timing', user => 'D'},

        preview => sub
           {my $o = shift;
            print '<p class="note">(This is only a preview. Accept the HIT before beginning to work.)</p>';
            menu_page $o, 0, 0, enum1 shuffle @pictures;},

        after_consent_prep => sub
           {my $o = shift;
            $o->assign_permutation('cheesecake_permutation', '|',
                @pictures);},

        footer => "\n\n\n</body></html>\n",

        mturk => $p{mturk}, assume_consent => $p{assume_consent});

    foreach my $choice (1, 2, 3)
       {my @ds = enum1 split /\|/, $o->getu('cheesecake_permutation');
        for (my $prev_choice = 1 ; $prev_choice < $choice ; ++$prev_choice)
          # Remove previously-chosen items from the lineup.
           {my $d = $o->getu("choice$prev_choice");
            @ds = grep {$_->[0] != $d} @ds;}
        menu_page $o, $choice == 1, $choice, @ds;}

    $o->buttons_page('gender',
        '<p>Are you male or female?</p>',
        'Male', 'Female');

    $o->yesno_page('attracted_to_women',
        '<p>Are you sexually attracted to women?</p>');

    $o->text_entry_page('comments',
        q(<p>(OPTIONAL) If you have any comments about this study that you'd like to make, you may enter them below.</p>),
        multiline => 1,
        accept_blank => 1,
        max_chars => 7 * 500);
          # Room for at least 500 words.

    $o->completion_page;};

defined $@ or $@ = 'Hit bottom with undefined $@';
$@ eq '' and $@ = 'Hit bottom with $@ set to the null string';

warn $@;

print "<p>Error:</p><pre>", htmlsafe($@), "</pre><p>Please report this.</p>\n</body></html>\n";

__END__
Content-Type: text/html; charset=utf-8

<!DOCTYPE html PUBLIC "-//W3C//DTD HTML 4.01//EN"
   "http://www.w3.org/TR/html4/strict.dtd">
<html lang="en-US">
<head>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
    <title>Sex-Appeal Rating</title>
    <style type="text/css">

    h1, form, p.stimulus, div.expbody p
       {text-align: center;}

    div.expbody p.long
       {text-align: left;}

    p.note
       {font-style: italic;}

    input.consent_statement
       {border: thin solid black;
        background-color: white;
        color: black;
        margin-bottom: .5em;}

    div.multiple_choice_box
       {display: table;
        margin-left: auto; margin-right: auto;}
    div.multiple_choice_box > div.row
       {display: table-row;}
    div.multiple_choice_box > div.row > div
       {display: table-cell;}
    div.multiple_choice_box > div.row > div.button
       {vertical-align: middle;
        padding-right: 1em;}
    div.multiple_choice_box > div.row > div.body
       {text-align: left;
        max-width: 200 px;}
    div.multiple_choice_box > div.row > div.body > div
       {display: table-cell;
        vertical-align: middle;}

    img.cheesecake
       {border: thin solid black;}

    textarea.text_entry
       {width: 90%;}

   </style>
</head>

<body>
